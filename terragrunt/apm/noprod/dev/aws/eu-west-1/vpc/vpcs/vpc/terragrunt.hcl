terraform {
  source = "${get_env("TG_ROOT_DIR", "/terragrunt")}/modules/aws//vpc"
}

include {
  path = find_in_parent_folders()
}

locals {
  common_vars = merge(
    yamldecode(file("${find_in_parent_folders("application.yaml")}")),
    yamldecode(file("${find_in_parent_folders("environment.yaml")}")),
    yamldecode(file("${find_in_parent_folders("provider.yaml")}")),
    yamldecode(file("${find_in_parent_folders("account.yaml")}")),
    yamldecode(file("${find_in_parent_folders("region.yaml")}")),
  )

  tags = merge({
    Application_name     = lookup(yamldecode(file("${find_in_parent_folders("application.yaml")}")), "application", ""),
    Environment          = lookup(yamldecode(file("${find_in_parent_folders("environment.yaml")}")), "environment", ""),
    Provider             = lookup(yamldecode(file("${find_in_parent_folders("provider.yaml")}")), "provider", ""),
    Account_id           = lookup(yamldecode(file("${find_in_parent_folders("account.yaml")}")), "account_id", ""),
    Region               = lookup(yamldecode(file("${find_in_parent_folders("region.yaml")}")), "region", ""),
    Confidentiality      = lookup(lookup(yamldecode(file("${find_in_parent_folders("application.yaml")}")), "static_resource_tags", ""), "confidentiality", ""),
    Owner                = lookup(lookup(yamldecode(file("${find_in_parent_folders("application.yaml")}")), "static_resource_tags", ""), "owner", ""),
    Created_by           = lookup(lookup(yamldecode(file("${find_in_parent_folders("application.yaml")}")), "static_resource_tags", ""), "created_by", ""),
    TG_resource_location = substr("${get_terragrunt_dir()}", 12, 0)
    },
    {
      # Always include description tag !!!
      Description = "${local.common_vars.environment} VPC",
      Name        = "${local.common_vars.environment}-vpc"
    }
  )
}

dependencies {
  paths = ["../../../s3/vpc_flow_logs"]
}

dependency "vpc_flow_logs_bucket" {
  config_path = "../../../s3/vpc_flow_logs"
  mock_outputs = {
    arn = "arn:aws:s3:::fake-bucket"
  }
}

inputs = {
  cidr_block               = local.common_vars.vpcs_vpc_cidr
  vpc-flow-logs_bucket_arn = dependency.vpc_flow_logs_bucket.outputs.arn
  # Tags are required (when possible)
  tags = local.tags
}